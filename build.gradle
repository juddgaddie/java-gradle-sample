buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    defaultTasks 'check', 'build', 'shadowJar'

    version = System.env.BUILD_NUMBER ?: 'SNAPSHOT'

}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'com.github.johnrengelman.shadow'


    apply from: "$rootDir/gradle/deps.gradle"

    shadowJar {
        destinationDir = new File("$rootDir/artifacts/${project.name}/")
        classifier = 'uber'
    }

    task unitTest(type: Test) {
        exclude "**/*AcceptanceTest.class"
        exclude "**/*IntegrationTest.class"
    }

    task integrationTest(type: Test) {
        include "**/*IntegrationTest.class"
    }

    task daoTest(type: Test) {
        include "**/*DaoTest.class"
        // Do not cache test results
        outputs.upToDateWhen { return false }
    }

    repositories {
        mavenCentral()
    }

    checkstyle {
        configFile = new File(rootDir, "config/checkstyle.xml")
    }

    configurations.checkstyle {
        transitive = true
    }

    configurations.findbugs {
        transitive = true
    }

}



configurations.all {
    transitive = false
}

